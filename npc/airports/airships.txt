//===== rAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

	OnTouch_:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","เรากำลังจะไปที่ Einbroch",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","เรากำลังจะลงจอดที่ Einbroch ในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับสู่ Einbroch ขอให้เดินทางปลอดภัย",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","ในตอนนี้เราอยู่ที่ Einbroch ยานเหาะจะออกบินในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ยานเหาะออกบินแล้ว สถานีต่อไปคือ Lighthalzen",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","เรากำลังจะไปที่ Lighthalzen",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","เรากำลังจะถึง Lighthalzen ในไม่ช้า",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับสู่ Lighthalzen ขอให้เดินทางปลอดภัย",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","ในตอนนี้เราอยู่ที่ Lighthalzen ยานเหาะจะออกบินในไม่ช้า",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ยานเหาะจะออกจากพื้นดินแล้ว สถานีต่อไปคือ Einbroch",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","เรากำลังจะไปที่ Einbroch",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","เรากำลังจะถึง Einbroch ในไม่ช้า",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับสู่ Einbroch ขอให้เดินทางปลอดภัย",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","ในตอนนี้เราอยู่ที่ Einbroch ยานเหาะจะออกบินในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ยานเหาะออกบินแล้ว สถานีต่อไปคือ Juno",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","เรากำลังจะไปที่ Juno",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","เรากำลังจะถึง Juno ในไม่ช้า",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับสู่ Juno ขอให้เดินทางปลอดภัย",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","ในตอนนี้เราอยู่ที่ Juno ยานเหาะจะออกบินในไม่ช้า",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ยานเหาะจะออกจากพื้นดินแล้ว สถานีต่อไปคือ Hugel",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","เรากำลังจะไปที่ Hugel",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","เรากำลังจะถึง Hugel ในไม่ช้า",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับสู่ Hugel ขอให้เดินทางปลอดภัย",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","ในตอนนี้เราอยู่ที่ Hugel ยานเหาะจะออกบินในไม่ช้า",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ยานเหาะจะออกจากพื้นดินแล้ว สถานีต่อไปคือ Juno",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","เรากำลังจะไปที่ Juno",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","เรากำลังจะถึง Juno ในไม่ช้า",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","ยินดีต้อนรับสู่ Juno ขอให้เดินทางปลอดภัย",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","ในตอนนี้เราอยู่ที่ Juno ยานเหาะจะออกบินในไม่ช้า",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","ยานเหาะจะออกจากพื้นดินแล้ว สถานีต่อไปคือ Einbroch",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Exit#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Exit#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Exit#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Exit#airplane2b	857

airplane,100,69,3	script	Airship Crew#ein-1	852,{
	mes "[Airship Crew]";
	mes "ถ้าเราลงจอดยังที่หมายของท่านและท่านจะออกจากยานเหาะโปรดใช้บันไดที่อยู่ข้างหน้านี้ ขอบคุณที่ใช้บริการของเรา";
	close;
}

airplane,64,94,1	script	Umbala Kid#ein_p	787,{
	emotion e_swt2;
	mes "[เด็ก]";
	if (event_umbala >= 3) {
		mes "โว้ว แม่!";
		mes "ด-ดูนี่สิ!";
		mes "เรากำลังบินอยู่! เ-เราคือ...";
		mes "เราคือท้องฟ้าโว้ยยย!";
	}
	else {
		mes "มาคุมาลากุ!";
		mes "ซัมป้า โจจิมโบะ";
		mes "คาคุ นา เจได โซโล่";
		mes "ฮร่าาาาาาาาาาาาาาาาาาาาาาาาา!";
	}
	close;
}

airplane,66,93,3	script	สาวน้อยจาก Umbala#ein_p	783,{
	emotion e_dots;
	mes "[สาวน้อย]";
	if (event_umbala >= 3) {
		mes "ชรุสส...";
		mes "ที่รัก หยุดสักที~";
		mes "อย่าทำเป็นว่ามันน่าตื่นเต้น";
		mes "เมื่อเราออกมายังที่สาธารณะแบบนี้!";
	}
	else {
		mes "ช๊กตรุ!";
		mes "สคะรับ มาลตริ!";
		mes "ชะกะ กรุ๊บต๊าฟริ";
		mes "ฮันติคริ มานโจว์!";
	}
	close;
}

airplane,71,91,7	script	ชายจาก Umbala#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[เชริมริม]";
		mes "ไง~";
		mes "ดูจากสีหน้าท่านแล้ว ข้าเห็นว่าท่านสามารถเข้าใจข้า";
		mes " ^333333*วิ๊ว...!*^000000";
		next;
		emotion e_pif;
		mes "[เชริมริม]";
		mes "ข้าได้ช่วยยานเหาะโดยการฝึกฝน ลูกเรือยานเหาะที่ Umbala เพื่อลดความกลัวที่เกิดจากการกระโดดบั้นจี้จั้มป์ มันเทพไปเลยใช่มั้ยล่ะ เอ๋?";
		next;
		mes "[เชริมริม]";
		mes "แต่...";
		mes "บางคนไม่สามารถผ่านความกลัวความสูงนั้นได้ และบางคนก็จบลงที่ เอิ่ม ^333333Nifflheim^000000";
	}
	else {
		mes "[เชริมริม]";
		mes "มาโจ๊ว กา";
		mes "นุ๊กตา ยานเหาะ";
		next;
		mes "[เชริมริม]";
		mes "...";
		mes "......";
		next;
		emotion e_pif;
		mes "[เชริมริม]";
		mes "ชาบาลา โมว บาจะมะ";
		mes "ยานเหาะ คุลุฮ่า โกโกนา";
		mes "ซาลุ ดามา โคโค่ นา นุ";
		mes "ยุคคุตะ เอิ่มมม ฟาซาคุ นา เร็ต!";
	}
	close;
}

airplane,250,58,2	script	พนักงานยานเหาะ#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[พนักงานยานเหาะ]";
		mes "ยินดีต้อนรับสู่ยานเหาะ ให้ข้าช่วยอะไรท่านไหม?";
		next;
		select("ท่านมีผู้โดยสารชื่อ เธียรี่ หรือป่าว?");
		mes "[พนักงานยานเหาะ]";
		mes "ข้าขอโทษนะ แต่ข้าไม่คิดว่าเรามีผู้โดยสารชื่อนั้น";
		close;
	}
	// Hugel quest end
	mes "[พนักงานยานเหาะ]";
	mes "ยินดีต้อนรับสู่ยานเหาะ ให้ข้าช่วยอะไรท่านไหม?";
	next;
	switch (select("ใช้ยานเหาะ:ห้องกัปตัน:สิ่งอำนวยความสะดวก:ยกเลิก")) {
	case 1:
		mes "[พนักงานยานเหาะ]";
		mes "เมื่อท่านเห็นประกาศว่าเราถึงที่หมายโปรดใช้ทางออกที่อยู่ทิศเหนือและทิศใต้ของยานเหาะ";
		next;
		mes "[พนักงานยานเหาะ]";
		mes "ถ้าท่านพลาดที่จะก้าวลง ไม่ต้องห่วงยานเหาะจะดำเนินไปเรื่อย ๆ และท่านจะมีโอกาสลงอีกครั้งอย่างแน่นอน";
		close;
	case 2:
		mes "[พนักงานยานเหาะ]";
		mes "ห้องกัปตันอยู่ที่ด้านหน้าสุดของยานเหาะ ที่นั่นท่านจะพบกัปตันและนักบินของยานเหาะ";
		close;
	case 3:
		mes "[พนักงานยานเหาะ]";
		mes "ในยานเหาะเรามี มินิเกม สำหรับให้ความบันเทิงแก่ผู้โดยสาร เราเชิญชวนให้ท่านลองวัดดวงและฝีมือของท่านในมินิเกมของเรา~";
		close;
	case 4:
		mes "[พนักงานยานเหาะ]";
		mes "โอเค ข้าขอให้ท่านเดินทางปลอดภัย ขอบคุณมาก ๆ ขอให้โชคดี";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[เซอร์ต้า]";
	mes "โอ้ ว่าไงนักพจญภัย";
	mes "ข้ากำลังเดินตามทางตามพระเจ้าอยู่ เป็นคำขอร้องจากนักบุญเพื่ออาณาจักร Rune-Midgard ที่ดีขึ้น";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[เมลเดอริน]";
	mes "อืมม ยานเหาะกำลังไปที่ Lutie หรือไม่ใช่? ข้ารอมานานมากล่ะ แต่ไม่เห็นจะมีประกาศว่าลง Lutie เลยแฮะ";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[อาโนส์]";
	mes "โอ้ ว๊าว~";
	mes "ท้องฟ้าดูต่างจากข้างล่างเมื่อเราอยู่ข้างบนนี้!";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[นักบิน]";
		mes "ข้าหวังว่าข้าจะได้ไปดื่มเบียร์เย็น ๆ การดื่มคือจุดมุ่งหมายในชีวิตของข้า! การดื่มทำให้ข้ามีพละกำลัง! ข้าจะไม่มีตัวตนหากไม่ได้ดื่ม!";
		next;
		mes "[นักบิน]";
		mes "แต่! การขับในระหว่างเมามันไม่ดี";
		mes "แต่! นั่นทำให้ข้าอยากดื่มมากขึ้นไปอีก!";
		emotion e_sob;
		next;
		select("ท่านรู้จักผู้โดยสารชื่อ เธียรี่ บ้างไหม?");
		mes "[นักบิน]";
		mes "ชุดนี่มันเล็กจังวุ้ย แต่มันก็ใหญ่ไปที่จะเดินไปมาบนยานเหาะอยู่ดี";
		next;
		mes "[นักบิน]";
		mes "...";
		mes "......";
		mes "ออ อ่อ ไม่เคยมีใครเดินเข้ามาในห้องนี้นะ";
		mes "และกัปตันเราก็คือกวางเรนเดียร์ ข้าถอดกางเกงในแปป";
		next;
		emotion e_omg;
		mes "[นักบิน]";
		mes "ว๊าาา!? ใครวะนั่น!";
		next;
		mes "- ...เขาไม่ได้ฟังคุณเลยแม้แต่น้อย -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[นักบิน]";
		mes "มันนานนนนนนน มาแล้วเมื่อข้ามีความสุข, ความเย็นของเหล้าเบียร์..แต่มันมีงานที่ต้องทำ..และมันทำให้ห่างจากเบียร์!";
		next;
		mes "[นักบิน]";
		mes "ข้าอยากที่จะดื่ม ข้าจำไม่ได้เลยว่าครั้งล่าสุดที่ได้หยุดเนี่ยมันวันไหน เฮ้อ เออใช่ การได้หยุดพัก กินขนม ได้นอนดูทีวีและอีกมากหมายที่ข้าอยากทำ";
		emotion e_sob;
		close;
	case 2:
		mes "[นักบิน]";
		mes "เพื่อน อากาศมันดีมากเลยนะวันนี้ สว่าง ท้องฟ้าเปิด มองเห็นได้ทั่ว ปลอดภัยสำหรับการบิน";
		close;
	case 3:
		mes "[นักบิน]";
		mes "ท่านรู้ไหม กัปตันของเราเป็นคนที่น่านับถือ เขาและพี่ชายของเขาดีมากและเป็นที่รู้จักในวงการยานเหาะ ใครจะไปรู้ว่ากวางเรนเดียร์เป็นกัปตันที่ดีได้?";
		next;
		mes "[นักบิน]";
		mes "แค่ท่านและข้า ข้าจะบอกท่าน ซานต้ากำลังทำอะไรบางอย่าง เอากวางเรนเดียร์และเหล่าเอลฟ์ไปทำงานให้เขา ผู้ชายคนนี้ฉลาดจริง ๆ !";
		close;
	default:
		mes "[นักบิน]";
		mes "ท่านรู้ไหม นักบินทั้งหมดในอากาศนี้เป็นนักบินใหม่ ท่านรู้ไหม? ช่ายยยย พวกเขาได้ทำงานกับยานเหาะนี้เร็วมาก";
		next;
		emotion e_omg;
		mes "[นักบิน]";
		mes "แล้วก็ พวกเขาดูจริงจัง คิดสำหรับการดำเนินงาน ข้าหมายถึง พวกเขาได้ฝึกฝนจากเราน่ะ มันดูเยี่ยมไปเลยใช่มั้ยล่ะ?!";
		close;
	}
}

airplane,50,66,5	script	Apple Merchant#airplane	86,{
	mes "[ฟรุ๊สต์]";
	mes "ยินดีต้อนรับสู่ ฟรุ๊สต์";
	mes "ร้านค้าที่คุณสามารถซื้อแอปเปิ้ลหรือจะทำเป็นน้ำแอปเปิ้ลก็ได้";
	next;
	switch (select("ซื้อแอปเปิ้ล:ทำน้ำแอปเปิ้ล:ทำไมท่านมาอยู่ที่นี่?:ยกเลิก")) {
	case 1:
		mes "[ฟรุ๊สต์]";
		mes "โปรดใส่จำนวนที่ท่านต้องการ ลูกละ 15 zeny";
		mes "ซื้อได้มากสุด 500 ลูกต่อครั้ง";
		mes "โปรดใส่เลข 0 หากไม่ต้องการซื้อ";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[ฟรุ๊สต์]";
				mes "ขอบคุณที่แวะมา ขอให้โชคดี มาใหม่ได้ตลอดนะ ท่านจะได้มีแอปเปิ้ลไว้เคี้ยวเล่น~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[ฟรุ๊สต์]";
				mes "ท่านใส่จำนวนมากกว่า 500 ไม่ได้โปรดใส่ใหม่";
				next;
			}
			else {
				mes "[ฟรุ๊สต์]";
				mes "แอปเปิ้ลทั้งหมด ^FF0000" + .@input + "^000000 ลูก";
				mes "ราคา ^FF0000" + .@pay + " Zeny^000000";
				mes "ดำเนินการต่อหรือไม่?";
				next;
				if (select("ใช่:ไม่ใช่") == 2) {
					mes "[ฟรุ๊สต์]";
					mes "ขอบคุณที่แวะมา ขอให้โชคดี มาใหม่ได้ตลอดนะ ท่านจะได้มีแอปเปิ้ลไว้เคี้ยวเล่น~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[ฟรุ๊สต์]";
			mes "ข้าขอโทษ แต่ท่านไม่มีเงินพอสำหรับซื้อแอปเปิ้ล โปรดเช็คเงินของท่านหรือซื้อแอปเปิ้ลให้น้อยกว่านี้";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[ฟรุ๊สต์]";
			mes "ฮืมมมม ข้าไม่คิดว่าท่านไม่มีที่ว่างพอในกระเป๋าท่าน โปรดทำให้กระเป๋าของท่านว่างกว่านี้";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[ฟรุ๊สต์]";
			mes "ขอบคุณที่แวะมาซื้อนะ ข้าหวังว่าท่านจะอร่อยไปกับแอปเปิ้ลพวกนี้~!";
			close;
		}
	case 2:
		mes "[ฟรุ๊สต์]";
		mes "โอเค ข้าต้องการ";
		mes "^FF00003 Apples และ 1 Empty Bottle^000000";
		mes "เพื่อสร้าง 1 Apple Juice ให้ท่าน";
		mes "ท่านจะดำเนินการต่อหรือไม่?";
		next;
		switch (select("ใช่:ไม่ใช่")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[ฟรุ๊สต์]";
				mes "ข้าขอโทษนะ แต่ท่านไม่มีส่วนผสมตามที่ข้าบอก";
				close;
			}
			else {
				mes "[ฟรุ๊สต์]";
				mes "ขอบคุณนะ โปรดรอสักครู่";
				next;
				mes "^3355FF*ปั่น ๆ ปั่น ๆ*";
				mes "*ปั่น ๆ ปั่น ๆ*";
				mes "*เคร๊ง...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[ฟรุ๊สต์]";
				mes "นี่ไง ได้แล้ว~";
				mes "ข้าหวังว่าท่านจะชอบนะ!";
				mes "มาใหม่ได้เสมอ นักพจญภัยอันหาญกล้า~";
				close;
			}
		case 2:
			mes "[ฟรุ๊สต์]";
			mes "ขอบคุณสำหรับการแวะร้านของข้า ขอให้โชคดี!";
			mes "มาใหม่ได้ตลอดเวลาเมื่อท่านรู้สึกอยากเคี้ยวแอปเปิ้ล~";
			close;
		}
	case 3:
		mes "[ฟรุ๊สต์]";
		mes "ข้าเคยเป็น คนเร่ร่อน มาก่อน วันหนึ่งข้างีบหลับและบางอย่างมากระทบหัวข้าและข้าก็ตื่นขึ้นมาในสลัม";
		next;
		mes "[ฟรุ๊สต์]";
		mes "มันเปลี่ยนชีวิตข้า การนอนหลับใต้ต้นแอปเปิ้ลและลูกแอปเปิ้ลตกลงมาโดนหัวข้า ข้าหิวและกินแอปเปิ้ลนั้น...";
		next;
		mes "[ฟรุ๊สต์]";
		mes "แต่สุดท้าย เครน เพื่อนเก่าข้าจากวันที่ไปขุดเหมือง ถามข้าเพื่อให้ไปช่วยเขาหน่อยบนยานเหาะ แล้วข้าก็ตกลง";
		mes "และมันทำให้ข้ามาที่นี่ และข้าเห็นผู้คนกำลังเล่นเกมทอยลูกเต๋า";
		next;
		mes "[ฟรุ๊สต์]";
		mes "ข้าเบื่อและจบลงที่การพนันด้วยแอปเปิ้ล แต่บางอย่างมันทำให้ข้ามีโชคลาภต่อเนื่อง แอปเปิ้ลหนึ่งลูกกลายเป็นสองลูก... ";
		next;
		mes "[ฟรุ๊สต์]";
		mes "สองลูกกลายเป็นสี่เหลือง และก่อนที่ข้าจะรู้ ข้ามีแอปเปิ้ลจำนวนมากมายมหาศาล!";
		mes "ข้าชนะ จนมีแอปเปิ้ลล้นหลาม ข้าเริ่มธุรกิจของข้าที่นี่บนยานเหาะ ดูแย่เหรอ ฮึ?";
		next;
		mes "[ฟรุ๊สต์]";
		mes "งั้น แอปเปิ้ลก็คงจะดีต่อพวกท่าน และมันก็ดีต่อข้าด้วย มาก ๆ เลยล่ะ ฮ่ะ ๆ ฮ่าาาา ฮ่า~!";
		close;
	case 4:
		mes "[ฟรุ๊สต์]";
		mes "ขอบคุณสำหรับการแวะเวียนร้านค้าของข้า ขอให้โชคดี~";
		close;
	}
}

//============================================================ 
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
	OnTouch_:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
		warp "izlude",200,73;
		else
		warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","เรากำลังจะไปที่ Izlude",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","เรากำลังจะถึง Izlude ในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","ยินดีต้อนรับสู่ Izlude ขอให้เดินทางปลอดภัย",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","เรากำลังอยู่ใน Izlude ยานเหาะจะออกบินในไม่ช้า",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","ยานเหาะออกบินแล้ว สถานีต่อไปคือ Juno",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","เรากำลังจะไปที่ Juno",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","เรากำลังจะถึง Juno ในไม่ช้า",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","ยินดีต้อนรับสู่ Juno ขอให้เดินทางปลอดภัย",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","เรากำลังอยู่ใน Juno ยานเหาะจะออกบินในไม่ช้า",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","ยานเหาะจะออกจากพื้นดินแล้ว สถานีต่อไปคือ Rachel",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","เรากำลังจะไปที่ Rachel",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","เรากำลังจะถึง Rachel ในไม่ช้า",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","ยินดีต้อนรับสู่ Rachel ขอให้เดินทางปลอดภัย",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","เรากำลังอยู่ใน Rachel ยานเหาะจะออกบินในไม่ช้า",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","ยานเหาะออกบินแล้ว สถานีต่อไปคือ Izlude",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Exit#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Exit#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Exit#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Exit#airplane_012b	857

airplane_01,250,58,2	script	Airship Staff#airplane01	67,{
	mes "[Airship Staff]";
	mes "ยินดีต้อนรับสู่ยานเหาะ";
	mes "ให้ข้าช่วยอะไรท่านไหม?";
	next;
	switch (select("ใช้ยานเหาะ:ห้องกัปตัน:สิ่งอำนวยความสะดวก:ยกเลิก")) {
	case 1:
		mes "[พนักงานยานเหาะ]";
		mes "เมื่อท่านเห็นประกาศว่าเราถึงที่หมายโปรดใช้ทางออกที่อยู่ทิศเหนือและทิศใต้ของยานเหาะ";
		next;
		mes "[พนักงานยานเหาะ]";
		mes "ถ้าท่านพลาดที่จะก้าวลง ไม่ต้องห่วงยานเหาะจะดำเนินไปเรื่อย ๆ และท่านจะมีโอกาสลงอีกครั้งอย่างแน่นอน";
		close;
	case 2:
		mes "[พนักงานยานเหาะ]";
		mes "ห้องกัปตันอยู่ที่ด้านหน้าสุดของยานเหาะ ที่นั่นท่านจะพบกัปตันและนักบินของยานเหาะ";
		close;
	case 3:
		mes "[พนักงานยานเหาะ]";
		mes "ในยานเหาะเรามี มินิเกม สำหรับให้ความบันเทิงแก่ผู้โดยสาร เราเชิญชวนให้ท่านลองวัดดวงและฝีมือของท่านในมินิเกมของเรา~";
		close;
	case 4:
		mes "[พนักงานยานเหาะ]";
		mes "โอเค ข้าขอให้ท่านเดินทางปลอดภัย ขอบคุณมาก ๆ ขอให้โชคดี";
		close;
	}
}

airplane_01,50,66,5	script	Apple Merchant#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- รอสักเดี๋ยวนะ !! -";
		mes "- ตอนนี้คุณมีน้ำหนักเกินที่จะแบกอะไรเพิ่มเติม -";
		mes "- ไอเท็มมากล้นเหลือเกินไปปป -";
		mes "- โปรดมาหาข้าใหม่ -";
		mes "- เมื่อคุณมีที่ว่างในกระเป๋าแล้ว -";
		close;
	}
	mes "[เมร๊ล]";
	mes "ยินดีต้อนรับสู่ร้านของ เมร๊ล";
	mes "ที่ที่คุณสามารถซื้อแอปเปิ้ลหรือทำน้ำแอปเปิ้ล";
	next;
	switch (select("ซื้อแอปเปิ้ล:ทำน้ำแอปเปิ้ล:ยกเลิก")) {
	case 1:
		mes "[เมร๊ล]";
		mes "โปรดใส่จำนวนที่ท่านต้องการ ลูกละ 15 zeny";
		mes "ซื้อได้มากสุด 500 ลูกต่อครั้ง";
		mes "โปรดใส่เลข 0 หากไม่ต้องการซื้อ";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[เมร๊ล]";
				mes "ขอบคุณที่แวะมา ขอให้โชคดี มาใหม่ได้ตลอดนะ ท่านจะได้มีแอปเปิ้ลไว้เคี้ยวเล่น~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[เมร๊ล]";
				mes "ท่านใส่จำนวนมากกว่า 500 ไม่ได้โปรดใส่ใหม่";
				next;
			}
			else {
				mes "[เมร๊ล]";
				mes "แอปเปิ้ลทั้งหมด ^FF0000" + .@input + "^000000 ลูก";
				mes "ราคา ^FF0000" + .@pay + " Zeny^000000";
				mes "ดำเนินการต่อหรือไม่?";
				next;
				if (select("ใช่:ไม่ใช่") == 2) {
					mes "[เมร๊ล]";
					mes "ขอบคุณที่แวะมา ขอให้โชคดี มาใหม่ได้ตลอดนะ ท่านจะได้มีแอปเปิ้ลไว้เคี้ยวเล่น~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[เมร๊ล]";
			mes "ข้าขอโทษ แต่ท่านไม่มีเงินพอสำหรับซื้อแอปเปิ้ล โปรดเช็คเงินของท่านหรือซื้อแอปเปิ้ลให้น้อยกว่านี้";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[เมร๊ล]";
			mes "ฮืมมมม ข้าไม่คิดว่าท่านไม่มีที่ว่างพอในกระเป๋าท่าน โปรดทำให้กระเป๋าของท่านว่างกว่านี้";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[เมร๊ล]";
			mes "ขอบคุณที่แวะมาซื้อนะ ข้าหวังว่าท่านจะอร่อยไปกับแอปเปิ้ลพวกนี้~!";
			close;
		}
	case 2:
		mes "[เมร๊ล]";
		mes "โอเค ข้าต้องการ";
		mes "^FF00003 Apples และ 1 Empty Bottle^000000";
		mes "เพื่อสร้าง 1 Apple Juice ให้ท่าน";
		mes "ท่านจะดำเนินการต่อหรือไม่?";
		next;
		switch (select("ใช่:ไม่ใช่")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[เมร๊ล]";
				mes "ข้าขอโทษนะ แต่ท่านไม่มีส่วนผสมตามที่ข้าบอก";
				close;
			}
			else {
				mes "[เมร๊ล]";
				mes "ขอบคุณนะ โปรดรอสักครู่";
				next;
				mes "^3355FF*ปั่น ๆ ปั่น ๆ*";
				mes "*ปั่น ๆ ปั่น ๆ*";
				mes "*เคร๊ง...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[เมร๊ล]";
				mes "นี่ไงได้แล้ว~";
				mes "โปรดมาใหม่อีกครั้งเมื่อท่านต้องการ";
				close;
			}
		case 2:
			mes "[เมร๊ล]";
			mes "ขอบคุณสำหรับการแวะร้านของข้า ขอให้โชคดี!";
			mes "มาใหม่ได้ตลอดเวลาเมื่อท่านรู้สึกอยากเคี้ยวแอปเปิ้ล~";
			close;
		}
	case 3:
		mes "[เมร๊ล]";
		mes "ขอบคุณสำหรับการแวะร้านของข้า ขอให้โชคดี!";
		mes "มาใหม่ได้ตลอดเวลาเมื่อท่านรู้สึกอยากเคี้ยวแอปเปิ้ล~";
		close;
	}
}

airplane_01,221,158,2	script	Pilot#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[นักบิน]";
		mes "ลองจิจูด 131 องศา ทิศตะวันออก";
		mes "ละติจูด 37 องศา ทิศเหนือ";
		mes "เรากำลังอยู่ในสถานะที่ดี กัปตัน";
		close;
	case 2:
		mes "[นักบิน]";
		mes "ดูแล้วเหมือนวันนี้จะมีเมฆมาก ดูแล้วยากต่อการนำทาง เมื่อท้องฟ้าไม่โปร่งใส คาดว่าเราจะต้องพึ่งเรดาร์แล้วล่ะ";
		close;
	case 3:
		mes "[นักบิน]";
		mes "กัปตันเป็นคนดี และข้าไม่สามารถคิดถึงคนที่เก่งสำหรับสั่งการยานเหาะนี้ ฉะนั้น เขาเก่งมาก ที่เป็นคนขับ";
		next;
		mes "[^ff0000ทารอค^000000]";
		mes "^ff0000เฮ้...!^000000";
		mes "^ff0000คุยให้มันน้อย ๆ หน่อยเว้ย^000000";
		mes "^ff0000ขับให้มันดี ๆ!^000000";
		next;
		mes "[นักบิน]";
		mes "โ-โอเคครับท่าน!";
		mes "(เห็นมั้ย ข้าบอกแล้ว?)";
		close;
	default:
		mes "[นักบิน]";
		mes "ชุดนี้มันเล็กจริง ๆ แต่มันก็ใหญ่ไปสำหรับยานเหาะนี้อยู่ดี";
		next;
		mes "[นักบิน]";
		mes "...";
		mes "......";
		mes "ไม่มีใครเคยมาที่ห้องนี้";
		mes "และกัปตันของเราคือกวางเรนเดียร์";
		mes "ข้าเพิ่งจะถอดกางเกงใน";
		next;
		emotion e_omg;
		mes "[นักบิน]";
		mes "โอ้...! หวัดดีนะ!";
		mes "ส-สน-สนุกกับการบินมั้ย?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[ไดอาน่า]";
	mes "มันน่าเกลียด! ข้าไปเยี่ยมกัปตันและสิ่งที่ข้าเห็นคือกวางเรนเดียร์ โอ้! คุณคิดว่ามันยังไงล่ะ...";
	close;

	OnTouch_:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[เมนเดล]";
	mes "จากที่ข้าคาดไว้ ในเที่ยวบินนี้ มีอาหารระดับสามดาวอย่างดีที่สุด";
	mes "*เฮ้ออออออ* ข้าสมควรนำเชฟของข้ามา แล้วมีความสุขกับมื้ออาหารจริง ๆ";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	106,{
	mes "[นักดาบชิมิสุ]";
	mes "ในที่สุด หลังจากห้าปีแห่งการรอคอย... ข้าจะได้ล้างแค้น!";
	next;
	mes "[นักดาบชิมิสุ]";
	mes "ข้าเพิ่งจะ...";
	mes "มั่นใจว่านั่นไม่ได้ทำให้ข้าพลาด แต่ภายภาคหน้า ภายหน้านั้น จะต้องมีการล้างแค้นอย่างแน่นอน!";
	close;
}

//============================================================ 
//= Typing Challenge
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[นิลล์]";
	mes "ยินดีต้อนรับสู่การแข่งขัน";
	mes "^ff0000พิมพ์ดีด RO^000000.";
	mes "ท่านอยากเล่นมั้ย? แข่งกันพิมพ์ดีด?";
	next;
	switch (select("เล่น ^ff0000พิมพ์ดีด RO^000000:รายละเอียด:ดูอันดับ:ยกเลิก")) {
	case 1:
		mes "[นิลล์]";
		mes "โอเค เรามีผู้เข้าแข่งขันใหม่ โปรดพิมพ์เท่าที่เร็วที่สุดเท่าที่จะทำได้ โดยห้ามพิมพ์ผิด! มาเริ่มกันเลย~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
		"^3cbcbcBy the power of^000000",
		"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
		"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
		"^3cbcbcCoboman no chikara-yumei na^000000",
		"^3cbcbcI'm the king of All Weirdos! Now^000000",
		"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
		"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
		"^3cbcbcskemd^000000",
		"^3cbcbcandoorabambarambambambambamburanbamding^000000",
		"^3cbcbcchikara-daiookii na chikara da ze!^000000",
		"^3cbcbcyou know of my true power. Obey~!^000000",
		"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
		"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
		"",
		"",
		"^3cbcbcCOBO ON^000000",
		"",
		"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
		"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
		"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
		"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
		"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
		"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
		"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
		"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
		"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
		"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
		"^ff1493belief love luck grimace sweat rush^000000",
		"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
		"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
		"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
		"",
		"^ff1493kabamturubamdingding^000000",
		"^ff1493folktale rodimus optimus bumblebee^000000",
		"^800080`open'0Pen open? open!111OPENSESAME^000000",
		"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
		"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
		"hfjdkeldjsieldjshfjdjeiskdlefvbd",
		"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
		"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
		"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
		"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
		1250,
		1180,
		1380,
		1740,
		1440,
		1450;
		set .@wordtest, rand(7);
		next;
		mes "[นิลล์]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save1$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[นิลล์]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save2$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[นิลล์]";
			mes "อันดับของท่านคือ ^ff0000" + .@total_time + " วินาที^000000 และ";
			mes "ตัวอักษรทั้งหมด " + .@tasoo + " ตัว";
			next;
			if (.@tasoo >= 1300) {
				mes "[นิลล์]";
				mes "ฮืม การบันทึกนี้ไม่เคยมีใครพิมพ์ได้ ยกเว้นท่านจะโกงโปรดเล่นดี ๆ อย่าโกง";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[นิลล์]";
				mes "การบันทึกล่าสุด";
				mes "สร้างโดย ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "ทั้งหมด ^0000ff" + $050320_ein_typing + "^000000 ตัวอักษร";
				mes "อย่างไรก็ตาม ^ff0000" + strcharinfo(0) + "^000000";
				mes "ท่านทำอันดับใหม่ในครั้งนี้ ยินดีด้วย!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[นิลล์]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "คือการบันทึกอันดับในตอนนี้";
				mes "ด้วยทั้งหมด ^0000ff" + $050320_ein_typing + "^000000";
				mes "ตัวอักษร ลองทำให้ดีกว่าเขาให้ได้";
				close;
			}
		}
		else {
			mes "[นิลล์]";
			mes "โอ...";
			mes "ข้าขอโทษ แต่ท่านพิมพ์ผิด...";
			close;
		}
	case 2:
		mes "[นิลล์]";
		mes "การแข่งขัน ^ff0000พิมพ์ดีด RO^000000";
		mes "คือเกมที่ท่านต้องพิมพ์ให้เร็วที่สุด คนที่เร็วที่สุดจะถูกบันทึกจารึกไว้อย่างสมศักดิ์ศรี!";
		next;
		mes "[นิลล์]";
		mes "ข้าอยากให้ท่านรู้ว่าพิมพ์ไปเลยบรรทัดเดียวไม่ต้องกด Enter แค่คลิก OK";
		close;
	case 3:
		mes "[นิลล์]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "คืออันดับตอนนี้";
		mes "ด้วยทั้งหมด ^0000ff" + $050320_ein_typing + "^000000";
		mes "ตัวอักษร ลองทำให้ดีกว่าเขาให้ได้นะ~";
		close;
	case 4:
		mes "[นิลล์]";
		mes "ไม่ต้องเกรงใจกับการแข่งขัน";
		mes "^ff0000พิมพ์ดีด RO^000000";
		mes "ถามข้าได้ตลอดเวลา ข้าจะอยู่ที่นี่~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[คราลิส]";
	mes "ไง ข้าชื่อคราลิส~";
	mes "อยากวัดดวงด้วยแอปเปิ้ลมั้ย";
	mes "เกมง่าย ๆ เกมทอยเต๋า?";
	next;
	callfunc "applegamble","คราลิส";
	end;
}

function	script	applegamble	{
	.@npc_name$ = getarg(0);
	switch (select("เล่นเกมทอยเต๋า:เรียนรู้กติกา:ยกเลิก")) {
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "ข้ามีเกมทอยเต๋า เมื่อท่านต้องการเล่น แค่บอกกับข้า โอเคนะ?";
		close;
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "กฎนั้นง่ายมาก วางแอปเปิ้ลไว้ ได้มากสุด 50 แอปเปิ้ล";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "เงินท่านอาจหมดใช่มั้ยล่ะ ข้าเลยใช้แอปเปิ้ลแทน~";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "จากนั้น จะเริ่มโดยที่ข้าจะโยนลูกเต๋า 6 ด้าน เมื่อมันถึงตาท่านท่านก็โยนหลังจากนั้นเราจะมีให้เลือกว่าจะโยนต่อหรือไม่";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ตอนนี้ล่ะ สำคัญ ถ้าแต้มรวมมากกว่า 12 ท่านจะแพ้ แต่ถ้าไม่เกินแล้วแต้มใครเยอะกว่าก็ชนะไป เข้าใจนะ?";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ตอนนี้ ท่านคือคนแรกที่จะเลือกว่าโยนรอบที่ 3 มั้ยจากนั้นข้าก็อาจโยนรอบที่ 3 หรือไม่โยนก็ได้";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "เมื่อท่านชนะ ท่านจะได้รับแอปเปิ้ล 2 เท่า เท่าที่ท่านลงไว้ แต่ถ้าเสมอก็ไม่มีผลใด ๆ แต่ถ้าแพ้ท่านก็เสียแอปเปิ้ลทั้งหมดที่ท่านลงไว้ โอเคนะ?";
		close;
	case 1:
		break;
	}
	mes "["+ .@npc_name$ +"]";
	mes "โอ ท่านจะเล่นกับข้าแล้วใช่มั้ย จะลงแอปเปิ้ลกี่ลูกดีล่ะ?";
	mes "จำไว้นะได้มากสุด 50 ลูก ถ้าไม่เล่นให้ใส่ 0";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "ไม่เล่นล่ะรึ?";
			mes "ข้าเข้าใจ ๆ ข้าหวังว่าท่านจะเปลี่ยนใจ";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+ .@npc_name$ +"]";
			mes "ท่านไม่สามารถลงแอปเปิ้ลได้เกิน 50 ลูก";
			next;
			continue;
		}
		mes "["+ .@npc_name$ +"]";
		mes "อืม ตกลงวางแอปเปิ้ลไว้ ^FF0000"+.@amount+"^000000 ลูก";
		mes "โอเคนะ?";
		next;
		if (select("ใช่:ไม่ใช่") == 2) {
			mes "["+ .@npc_name$ +"]";
			mes "อื้มมม ลงผิดรึ โอเค เอาใหม่นะ จะลงเท่าไหร่ก็คิดดี ๆ";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "ข้าขอโทษแต่ท่านมีแอปเปิ้ลจำนวนไม่เท่ากับที่จะลง ตรวจจำนวนแอปเปิ้ลของท่านก่อนเล่นนะ";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+ .@npc_name$ +"]";
		mes "ดี!";
		mes "ตอนนี้เราสามารถเล่นเกมนี้ได้แล้ว! ข้าจะโยนก่อน~";
		break;
	}
	mes "^3355FF*โยน ๆ และ กวน ๆ*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+ .@npc_name$ +"]";
	mes "ข้าได้ ^0000FF" + .@table1 + "^000000 และ ^0000FF" + .@table2 + "^000000";
	mes "นั่นคือทั้งหมด ^0000FF" + .@tablesub + "^000000";
	mes "^FF0000" + strcharinfo(0) + "^000000 ตาท่านแล้ว";
	next;
	select("โยน");
	mes "^3355FF*โยน ๆ และ กวน ๆ*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+ .@npc_name$ +"]";
	mes "^FF0000" + strcharinfo(0) + "^000000 ท่านได้ ^FF0000" + .@player1 + "^000000 และ ^FF0000" + .@player2 + "^000000 ทั้งหมดคือ ^FF0000" + .@playersub + "^000000";
	next;
	mes "["+ .@npc_name$ +"]";
	if(.@playersub == .@tablesub) {
		mes "ตอนนี้ทั้งหมดคือ ^0000FF" + .@tablesub + "^000000 และ ^FF0000" + strcharinfo(0) + "^000000 ทั้งหมดของท่าน ^FF0000" + .@playersub + "^000000 เท่ากันอยู่ ท่านจะโยนใหม่มั้ย?";
	} else if (.@playersub > .@tablesub) {
		mes "ตอนนี้ทั้งหมดคือ ^0000FF" + .@tablesub + "^000000 และ ^FF0000" + strcharinfo(0) + "^000000 ทั้งหมดของท่าน ^FF0000" + .@playersub + "^000000 ^FF0000" + strcharinfo(0) + "^000000 ท่านกำลังชนะอยู่ท่านจะโยนใหม่อีกมั้ย?";
	} else if(.@tablesub > .@playersub) {
		mes "ตอนนี้ทั้งหมดคือ ^0000FF" + .@tablesub + "^000000 และ ^FF0000" + strcharinfo(0) + "^000000 ทั้งหมดของท่าน ^FF0000" + .@playersub + "^000000 ท่านกำลังจะแพ้ ท่านจะโยนใหม่อีกมั้ย?";
	}
	next;
	switch (select("โยน:ยกเลิก")) {
	case 1:
		mes "^3355FF*โยน ๆ และ กวน ๆ*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+ .@npc_name$ +"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000 ท่านได้ ^FF0000" + .@player3 + "^000000 และตอนนี้ทั้งหมดคือ ^FF0000" + .@playertotal + "^000000 ท่านแพ้แล้ว ขอโทษนะแต่ลองใหม่รอบหน้านะ";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000 ท่านได้ ^FF0000" + .@player3 + "^000000 และตอนนี้ทั้งหมดคือ ^FF0000" + .@playertotal + "^000000 เสมอแล้วท่านถอยได้น้อยกว่าข้าท่านแพ้แล้ว ลองใหม่นะ";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000 ท่านได้ ^FF0000" + .@player3 + "^000000 และตอนนี้ทั้งหมดคือ ^FF0000" + .@playertotal + "^000000 ข้าไม่ได้อยากจะเสี่ยง มาจบเกมด้วยการเสมอ ไว้มาเล่นกันใหม่~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "โอเค";
			mes "มาลองทอบเต๋ากันใหม่";
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000 ท่านได้ ^FF0000" + .@player3 + "^000000 และตอนนี้ทั้งหมดคือ ^FF0000" + .@playertotal + "^000000 ตอนนี้ตาข้า";
		}
		break;
	case 2:
		mes "["+ .@npc_name$ +"]";
		if (.@playersub > .@tablesub) {
			mes "รู้ล่ะ ๆ ท่านไม่อยากเสี่ยงที่จะแพ้ โอเค ให้ข้าโยนอีกรอบนะ";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "อ้ออออ ท่านไม่อยากเสี่ยงที่จะแพ้ ข้าก็เหมือนกัน มาจบด้วยการเสมอล่ะกัน ไว้มาเล่นกันใหม่~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "โอเค";
			mes "มาลองทอบเต๋ากันใหม่";
		}
		else {
			mes "มันรู้เจ็บใช่มั้ยล่ะ  ข้ารู้ แต่..ข้าชนะ..ไว้มาเล่นกันใหม่~";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*โยน ๆ และ กวน ๆ*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+ .@npc_name$ +"]";
	if (.@tabletotal > 12) {
		mes "ข้าได้ ^0000FF" + .@table3 + "^000000 และตอนนี้ทั้งหมดคือ ^0000FF" + .@tabletotal + "^000000 ข้าแพ้ แต้มข้าเกิน 12 ให้ข้ามอบแอปเปิ้ลให้ท่านนะ ยินดีด้วย มันสนุกมากเลยล่ะ";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "ข้าได้ ^0000FF" + .@table3 + "^000000 และตอนนี้ทั้งหมดคือ ^0000FF" + .@tabletotal + "^000000 ด้วยทั้งหมด ^FF0000" + .@playertotal + "^000000 ท่านชนะ ^FF0000" + strcharinfo(0) + "^000000 ให้ข้ามอบแอปเปิ้ล และนั่นสนุกมาก ข้าหวังว่าท่านจะมาเล่นกับข้าอีก";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "ข้าได้ ^0000FF" + .@table3 + "^000000 และตอนนี้ทั้งหมดคือ ^0000FF" + .@tabletotal + "^000000 ด้วยทั้งหมด ^FF0000" + .@playertotal + "^000000 เกมนี้เสมอ ^FF0000" + strcharinfo(0) + "^000000 เอาแอปเปิ้ลท่านกลับไปได้เลย มันเป็นเกมที่สนุกมาก และหวังว่าท่านจะเล่นกับข้าใหม่อีก";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "ข้าได้ ^0000FF" + .@table3 + "^000000 และตอนนี้ทั้งหมดคือ ^0000FF" + .@tabletotal + "^000000 ด้วยทั้งหมด ^FF0000" + .@playertotal + "^000000 ท่านแพ้ ^FF0000" + strcharinfo(0) + "^000000 ข้าขอโทษนะ ไว้ลองใหม่นะ";
		close;
	}
}
