//===== rAthena Script =======================================
//= Kunai Merchant Kashin
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=     id of the 2 required items plus the amount. Can trade
//=     up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.3a Kagerou/Oboro support (BaseJob -> BaseClass) [Euphy]
//============================================================

que_ng,72,29,3	script	Kunai Merchant Kashin	83,{
	if(BaseClass == Job_Ninja) {
		mes "[Kashin]";
		mes "ข้าชื่อคาชิน ผู้สร้าง Kunai ให้เหล่านินจา ลองมองดูสิและถ้าท่านสนใจอะไรบอกข้าได้เลย";
		next;
		switch(select("10 Fell Poison Kunai:10 Icicle Kunai:10 High Wind Kunai:10 Black Earth Kunai:10 Heat Wave Kunai:ยกเลิก")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[Kashin]";
			mes "โอเค ขอบคุณที่มาดูร้านของข้า โปรดมาหาข้าใหม่เมื่อท่านต้องการซื้อ Kunai ไว้เจอกันใหม่~";
			close;
		}
	}
	mes "[Kashin]";
	mes "ข้าชื่อคาชิน ผู้สร้าง Kunai ให้เหล่านินจา ถ้าท่านมีเพื่อนเป็นนินจาอยู่ ท่านน่าจะบอกพวกเขาให้มาหาข้า";
	close;
}

function	script	Kunai_Trade	{
	setarray .@item_req[0], getarg(0), getarg(2);
	setarray .@req_amount[0], getarg(1), getarg(3);
	.@item_id = getarg(4);

	mes "[Kashin]";
	mes "ท่านสามารถแลก";
	mes .@req_amount[0] +" "+ getitemname(.@item_req[0]) +" และ";
	mes .@req_amount[1] +" "+ getitemname(.@item_req[1]) +" สำหรับ";
	mes "10 "+ getitemname(.@item_id) +"";
	next;
	mes "[Kashin]";
	mes "ข้าสามารถให้ท่านได้มากสุด 500 เซ็ท Kunai ต่อครั้ง ถ้าท่านต้องการยกเลิก โปรดใส่ 0 แล้วท่านจะเอากี่เซ็ทดีล่ะ?";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[Kashin]";
		mes "เอ๋? ข้าไม่สามารถทำมากกว่า 500 เซ็ทต่อครั้งได้";
		close;
	}
	if (countitem(.@item_req[0]) < .@amount*.@req_amount[0] || countitem(.@item_req[1]) < .@amount*.@req_amount[1]) {
		mes "[Kashin]";
		mes "หืม ท่านมีไอเท็มไม่เพียงพอจะแลกเปลี่ยน โปรดเช็คของก่อน";
		close;
 	}
	if (checkweight(.@item_id, .@amount*10) == 0) {
		mes "[Kashin]";
		mes "หืม ท่านเหมือนจะแบกอะไรต่อไม่ไหวแล้วนะ กลับมาหาข้าใหม่เมื่อน้ำหนักในกระเป๋าท่านเบาลง";
		close;
	}
	mes "[Kashin]";
	mes "ดีมาก ทุกอย่างดูเรียบร้อยดี งั้นก็จบการแลกเปลี่ยนนี้ได้แล้ว ข้าหวังว่าท่านจะพึงพอใจกับ Kunai ของข้านะ";
	delitem .@item_req[0],.@req_amount[0]*.@amount;
	delitem .@item_req[1],.@req_amount[1]*.@amount;
	getitem .@item_id,10*.@amount;
	close;
}
