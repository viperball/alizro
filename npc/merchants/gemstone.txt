//===== rAthena Script ======================================= 
//= Gemstone Trader
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Trade various colors of gemstones for other color gemstones.
//===== Additional Comments: ================================= 
//= 1.0 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Any notes pertaining to the prior trader may be found
//=     in the cities/payon.txt
//= 1.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.2 Updated input with min/max values. [L0ne_W0lf]
//=     Added a checkweight. 
//= 1.3 Fixed checks. [Euphy]
//============================================================ 

payon,173,238,5	script	Jade#pay	754,{
	if (checkweight(1201,1) == 0) {
		mes "^3355FFเดี๋ยวนะ! ตอนนี้ท่านมีน้ำหนักในกระเป๋ามากเกินไป โปรดมาพบข้าใหม่เมื่อน้ำหนักในกระเป๋าท่านน้อยลง^000000";
		close;
	}
	mes "[Jade]";
	mes "นำ Gemstones มาให้ข้า 2 ชิ้น สีเหมือนกันด้วยนะ แล้วข้าจะเปลี่ยนมันเป็น สีอื่นได้";
	next;
	switch(select("Blue Gemstones ไปนัว Red Gemstones!:Red Gemstones ไปยัง Yellow Gemstones!:Yellow Gemstones ไปยัง Blue Gemstones!")) {
	case 1: callsub S_TradeGems,717,716;
	case 2: callsub S_TradeGems,716,715;
	case 3: callsub S_TradeGems,715,717;
	}

S_TradeGems:
	.@item_req = getarg(0);
	.@item_id = getarg(1);

	if (countitem(.@item_req) < 2) {
		mes "[Jade]";
		mes "หา...!";
		mes "ล้อเล่นใช่ป่ะ? ข้าทำให้ไม่ได้นะ ต้องมีอย่างน้อย";
		mes "2 "+ getitemname(.@item_req) +"s!";
		close;
	}
	else {
		.@gems = countitem(.@item_req) /2;
		mes "[Jade]";
		mes "ข้าเชื่อว่าข้าสามารถสร้าง "+ .@gems +" "+ getitemname(.@item_id) +"s";
		mes "โดยใช้ "+ getitemname(.@item_req) +"s";
		mes "นั่นคือทั้งหมดที่ท่านมี ท่านจะเอาไงต่อ?";
		next;
		switch(select("โปรดนำมันมาให้ข้าทั้งหมด:ข้าอยากจะเปลี่ยนจำนวนก่อน:ข้าไม่เอาแล้ว")) {
		case 1:
			delitem .@item_req,.@gems * 2;
			getitem .@item_id,.@gems;
			break;
		case 2:
			mes "[Jade]";
			mes "แล้วจะเอากี่อันล่ะ?";
			mes "มากสุด 100 อัน";
			next;
			while(1) {
				input .@input,0,101;
				if (.@input == 0) {
					mes "[Jade]";
					mes "ไม่เอาเลย? หวังว่าท่านจะเปลี่ยนใจภายหลังนะ...";
					close;
				}
				else if (.@input > 100) {
					mes "[เจดส์]";
					mes "เอิ่ม...";
					mes "ข้าบอกว่าอย่ามากเกิน 100 อัน จำได้ไหม...?";
					next;
				}
				else if (.@gems < .@input) {
					// Custom dialogue
					mes "[Jade]";
					mes "เอิ่ม...";
					mes "ท่านไม่มีของที่จะใช้แลกกับข้าเลยนะ...";
					next;
				}
				else break;
			}
			delitem .@item_req,.@input * 2;
			getitem .@item_id,.@input;
			break;
		case 3:
			mes "[Jade]";
			mes "แน่นอน ไม่เป็นไรหรอก มาหาข้าได้เสมอ";
			close;
		}
		mes "[Jade]";
		mes "อ่ะ นี่ไง เสร็จแล้ว มาหาข้าได้เมื่อท่านต้องการ ไปได้แล้ว..  หือ? มีอะไรติดที่หน้าข้ารึไง ไปสิ?";
		close;
	}
}
