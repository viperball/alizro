prontera,164,109,4	script	VFP Manager	62,{
	function getPoints;

	set .@n$, 			"[VFP Manager]";
	set .@settings, 	1;		// 0 = item, 1 = shop
	set .@itemReward, 	501;	// if settings is set to item
	set .@convertRate, 	2;		// 2 Vote Points for 1 Red Potion
	set .vp, 			getPoints(getcharid(3));
	
	switch (.@settings) {
		case 0:
			mes .@n$;
			mes "คุณต้องการเปลี่ยน "+ .vp +" Vote Points หรือเปล่า?";
			mes "^ff0000เรทสำหรับแลกเปลี่ยนตอนนี้คือ "+ .@convertRate +" Vote Points จะได้ 1 "+ getitemname(.@itemReward) +".";
			next;
			menu "ใช่", L_Convert, "ไม่", -;
			mes .@n$;
			mes "ขอบคุณที่ใช้บริการ";
			break;
		case 1:
			mes .@n$;
			mes "คุณมี ^ff0000"+ .vp +"^000000 Vote Points.";
			mes "ต้องการใช้มันซื้อของมั๊ย?";
			next;
			menu "ใช่", -, "ไม่", L_Goodbye;
			mes .@n$;
			mes "ขอให้เพลิดเพลินกับการชอปปิ้ง!";
			callshop "votepoints_shop",1;
			npcshopattach "votepoints_shop";
			end;
	}
	
	L_Goodbye:
		mes .@n$;
		mes "ขอบคุณที่ใช้บริการ";
		close;
	
	L_Convert:
		if (.vp < .@convertRate)
			goto L_VotePointsTooLow;
		mes .@n$;
		mes "ต้องการใช้ Vote Points ในการแลกเท่าไหร่?";
		next;
		menu "ทั้งหมด", L_ConvertAll, "ระบุจำนวน", L_ConvertAmount;
	
	L_ConvertAmount:
		input .@convert;
		
		if (.@convert > .vp)
			goto L_VotePointsTooLow;
		
		set .vp, ((.vp - .@convert) + (.@convert % .@convertRate));
		set .@convert, (.@convert / .@convertRate);
		getitem .@itemReward, .@convert;
		query_sql("UPDATE cp_createlog SET votepoints = "+ .vp +" WHERE account_id = "+ getcharid(3));
		
		mes .@n$;
		mes "คุณได้รับ "+ .@convert +"x "+ getitemname(.@itemReward) +".";
		mes "ตอนนี้ Vote Points ของคุณเหลือ "+ .vp;
		close;
		
	L_ConvertAll:
		set .@convert, (.vp / .@convertRate);
		set .vp, (.vp % .@convertRate);
		getitem .@itemReward, .@convert;
		query_sql("UPDATE cp_createlog SET votepoints = "+ .vp +" WHERE account_id = "+ getcharid(3));
		
		mes .@n$;
		mes "คุณได้รับ "+ .@convert +"x "+ getitemname(.@itemReward) +".";
		mes "ตอนนี้ Vote Points ของคุณเหลือ "+ .vp;
		close;
	
	L_VotePointsTooLow:
		mes .@n$;
		mes "คุณมี Vote Points น้อยเกินไปโปรดกลับมาใหม่เมื่อคุณมี Vote Points ที่พอสำหรับแลกเปลี่ยน";
		close;
	
	function getPoints {
		set .@account_id, getarg(0);
		
		set .@count, query_sql("SELECT votepoints FROM cp_createlog WHERE account_id = "+ .@account_id, .@votepoints);
		
		return .@count ? .@votepoints : 0;
	}
	
	OnBuyItem:
		set .@cost,0;
		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
			for(set .@j,0; .@j<getarraysize(.itemShop); set .@j,.@j+2)
				if (@bought_nameid[.@i] == .itemShop[.@j]) {
					set .@cost, .@cost+(.itemShop[.@j+1]*@bought_quantity[.@i]);
					break;
				}
		mes .@n$;
		if (.@cost > .vp) mes "คุณมี Vote Points ไม่เพียงพอ";
		else {
			for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1) {
				getitem @bought_nameid[.@i], @bought_quantity[.@i];
				dispbottom "Purchased "+@bought_quantity[.@i]+"x "+getitemname(@bought_nameid[.@i])+".";
			}
			set .vp, .vp - .@cost;
			query_sql("UPDATE cp_createlog SET votepoints = votepoints - "+ .@cost +" WHERE account_id = "+ getcharid(3));
			mes .@n$;
			mes "การแลกเปลี่ยนเสร็จสิ้น";
			mes "ตอนนี้คุณมี ^ff0000"+ .vp +"^000000 Vote Points.";
			emotion e_cash;
		}
		set .@cost,0;
		deletearray @bought_nameid[0], getarraysize(@bought_nameid);
		deletearray @bought_quantity[0], getarraysize(@bought_quantity);
		close;
	
	OnInit:
		setarray .itemShop[0],	501,2,607,5; // <ITEM_ID>,<PRICE>,...
		
		npcshopdelitem "votepoints_shop",512;
		for(set .@i, 0; .@i < getarraysize(.itemShop); set .@i, .@i+2)
			npcshopadditem "votepoints_shop", .itemShop[.@i], .itemShop[.@i+1];
	end;
}

-	shop	votepoints_shop	-1,512:-1s