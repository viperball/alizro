//===== rAthena Script =======================================================
//= Custom Battle Ground
//===== By: ==================================================================
//= Script: naLizn+
//===== Current Version: =====================================================
//= 1.0
//===== Compatible With: =====================================================
//= eAthena, rAthena
//===== Description: =========================================================
//= เลือกชนิดเกม ต่อสู้ ได้แก่ เกมทำทายหินกิล,  เกมทำลายหินฝั่งตรงข้าม, ช่วยตัวประกัน
//= ทีมชนะได้รางวัลทุกคน
//===== Contact me ===========================================================
//= https://discordapp.com/channels/243351250077220864/243351250077220864 ====
//============================================================================
-	script	bg_emp#control	-1,{
OnInit:
	.minplayer2start = 1;      // จำนวนผู้เล่นขั้นต่ำ เช่น 3vs3 ตั้งค่า เเป็น 3
	.eventlasting    = 20*60;  // ระยะเวลา
	setarray .rewarditem[0],   // รางวัลของทีมชนะ <item>,<amount>,...
		501, 10;
	.team1name$ = "Red";
	.team2name$ = "Blue";
	$@WaitingTime = 10;
	setarray $@montext$[0],"ขอบคุณมากที่มาช่วย~~","นึกว่าจะไม่รอดซะแล้ว","แถวนี้กลิ่นไม่ดีรีบไปกันเถอะ","ดีใจจัง จะได้กลับบ้านแล้ว","หนีกันศัตรูจะมาแล้ว","ท่านเดินไวๆสิ";
	disablenpc "Red Angel#bat";
	disablenpc "Blue Angel#bat";
	end;
OnStart:
	if ( getwaitingroomstate( 0, .rednpcname$ ) < .minplayer2start || getwaitingroomstate( 0, .bluenpcname$ ) < .minplayer2start )
		end;
	mapannounce "prontera", "กำลังจะส่งนักรบ ไปยังสนามรบ ใน", bc_map;
	sleep 1000;
	mapannounce "prontera", "3", bc_map;
	sleep 1000;
	mapannounce "prontera", "2", bc_map;
	sleep 1000;
	mapannounce "prontera", "1", bc_map;
	if ( getwaitingroomstate( 0, .rednpcname$ ) < .minplayer2start || getwaitingroomstate( 0, .bluenpcname$ ) < .minplayer2start ){
		mapannounce "prontera", "มีบางคนเกิดกลัวขึ้นมา ท่านจะต้องรอกันอีกสักพัก", bc_map;
		end;
	}	
	// สร้างทีม and teams
	.red = waitingroom2bg( "bat_a01", 157,347, strnpcinfo(0)+"::OnRedQuit", strnpcinfo(0)+"::OnRedDead", .rednpcname$ );
	copyarray .team1aid, $@arenamembers, $@arenamembersnum;
	.team1count = .minplayer2start;
	.blue = waitingroom2bg( "bat_a01", 142,51, strnpcinfo(0)+"::OnBlueQuit", strnpcinfo(0)+"::OnBlueDead", .bluenpcname$ );
	copyarray .team2aid, $@arenamembers, $@arenamembersnum;
	.team2count = .minplayer2start;
	
	set $@GameRunning,1;
	delwaitingroom .rednpcname$;
	delwaitingroom .bluenpcname$;
	disablenpc .rednpcname$;
	disablenpc .bluenpcname$;
	setwall "bat_a01", 164,347, 6, 4, 0, "bg_emp_town_red";
	setwall "bat_a01", 154,51, 6, 4, 0, "bg_emp_town_blue";
	bg_warp .red, "bat_a01", 171,346;
	bg_warp .blue, "bat_a01", 162,50;
	bg_updatescore "bat_a01", 0, 0;

	// delay before match begins
	sleep 6000;
	//-----------GAME RULE--------------------------------------
	if($@GamePlay==1){
		mapannounce "bat_a01", "กติกามีข้อเดียวง่ายๆ ทีมไหนตีหินแตกก่อนจะเป็นผู้ชนะ", bc_map;
	}
	if($@GamePlay==2){
		mapannounce "bat_a01", "กติกามีข้อเดียว บุกทำลายหินของศัตรู", bc_map;
	}
	if($@GamePlay==3){
		mapannounce "bat_a01", "ศัตรูจับสัตว์เลี้ยงเราไป บุกไปช่วยเร็ว...", bc_map;
	}
	//---------------------------------------------------------
	sleep 3000;
	for ( .@i = 5; .@i > 0; .@i-- ) {
		mapannounce "bat_a01", "["+ .@i +"]", bc_map;
		sleep 1000;
	}
	mapannounce "bat_a01", "เริ่มได้ !", bc_map;

	// ----------------  spawn Emperiums--------------------------------------------------
	if($@GamePlay==1){
		bg_monster .emperiums,"bat_a01",274,203, "Emperiums",1288, strnpcinfo(3)+"::OnEmpDown";
	}
	if($@GamePlay==2){
		bg_monster .red,"bat_a01",171,346, "--ja--",1915, strnpcinfo(3)+"::OnRedDown";
		bg_monster .blue,"bat_a01",162,50, "--ja--",1914, strnpcinfo(3)+"::OnBlueDown";
	}
	if($@GamePlay==3){
		enablenpc "Red Angel#bat";
		enablenpc "Blue Angel#bat";
	}
	//------------------------------------------------------------------------------------
	delwall "bg_emp_town_red";
	delwall "bg_emp_town_blue";

	// match duration
	sleep .eventlasting * 1000;

	// end match, destroy Battleground, reset NPCs----------------------------------------
	if($@GamePlay==1){
		killmonster "bat_a01", strnpcinfo(3)+"::OnEmpDown";
	}
	if($@GamePlay==2){
		killmonster "bat_a01", strnpcinfo(3)+"::OnRedDown";
		killmonster "bat_a01", strnpcinfo(3)+"::OnBlueDown";
	}
	//------------------------------------------------------------------------------------
	if ( .winside ) {
		mapannounce "bat_a01", "- ทีม ["+ getd( ".team"+ .winside +"name$" ) +"] ชนะ -", bc_map;
		for ( .@i = 0; .@i < getd(".team"+ .winside +"count"); .@i++ )
			getitem .rewarditem[0], .rewarditem[1], getd(".team"+ .winside +"aid["+ .@i +"]" );
	} else
		mapannounce "bat_a01", "- เกมจบแล้วและไม่มีผู้ชนะ -", bc_map;
	sleep 5000;
	bg_warp .red, "prontera", 155,182;
	bg_warp .blue, "prontera", 158,182;
	bg_destroy .red;
	bg_destroy .blue;
	delwall "bg_emp_town_red";
	delwall "bg_emp_town_blue";
	deletearray .team1aid;
	deletearray .team2aid;
	$@GamePlay = $@GameRunning = .winside = .team1count = .team2count = 0;
	
	if($@GamePlay==3){
		moveNPC "Blue Angel#bat",171,346;
		moveNPC "Red Angel#bat",162,50;
		
		disablenpc "Red Angel#bat";
		disablenpc "Blue Angel#bat";
	}
	end;

// Emperium destroyed
OnRedDown:  callsub L_EmpDown, 1, 2;
OnBlueDown: callsub L_EmpDown, 2, 1;
L_EmpDown:
	if($@GamePlay!=3) mapannounce "bat_a01", strcharinfo(0) +" ได้ทำลายหินของทีม ["+ getd( ".team"+ getarg(0) +"name$" ) +"]", bc_map;
	.winside = getarg(1);
	awake strnpcinfo(0);
	end;
	
OnEmpDown:
	mapannounce "bat_a01", strcharinfo(0) +" ได้ทำลาย Emperiums ลงแล้ว ", bc_map;
	set @tt,getcharid(4);
	@tt %= 2;
	if(@tt==0) @tt =2;
	.winside = @tt;
	awake strnpcinfo(0);
	end;

// "OnDeath" event
OnRedDead:
	sleep 1250;
	areapercentheal "bat_a01",154,349,160,343,100,100; //RED
	end;
	
OnBlueDead:
	sleep 1250;
	areapercentheal "bat_a01",140,53,144,48,100,100;  //BLUE
	end;

// "OnQuit" event
OnRedQuit:  callsub L_Quit, 1, 2;
OnBlueQuit: callsub L_Quit, 2, 1;
L_Quit:
	percentheal 100, 100;
	while ( getd( ".team"+ getarg(0) +"aid["+ .@i +"]" ) != getcharid(3) && .@i < getd(".team"+ getarg(0) +"count") ) .@i++;
	deletearray getd( ".team"+ getarg(0) +"aid["+ .@i +"]" ), 1;
	setd ".team"+ getarg(0) +"count", getd(".team"+ getarg(0) +"count") -1;
	if ( getd(".team"+ getarg(0) +"count") ) end;
	mapannounce "bat_a01", "ผู้เล่นทีม ["+ getd( ".team"+ getarg(0) +"name$" ) +"] ได้ออกจากเกมหมดแล้ว", bc_map, 0xff3333;
	end;
}

prontera,168,126,5	script	Red Team Lead#bg_emp	459,{
	end;
OnInit:
	sleep 1;
	set getvariableofnpc( .rednpcname$, "bg_emp#control" ), strnpcinfo(0);
	end;
OnStart:
	waitingroom "Red Team", getvariableofnpc( .minplayer2start, "bg_emp#control" ) +1, "bg_emp#control::OnStart", getvariableofnpc( .minplayer2start, "bg_emp#control" );
	end;
}

prontera,176,126,3	script	Blue Team Lead#bg_emp	868,{
	end;
OnInit:
	sleep 1;
	set getvariableofnpc( .bluenpcname$, "bg_emp#control" ), strnpcinfo(0);
	end;
OnStart:
	waitingroom "Blue Team", getvariableofnpc( .minplayer2start, "bg_emp#control" ) +1, "bg_emp#control::OnStart", getvariableofnpc( .minplayer2start, "bg_emp#control" );
	end;
}

prontera,172,126,4	script	Battle Organizer#prt	700,{
	mes "^0B7E54[ Battle Organizer ]^000000";
	if($@GamePlay!=0&&$@GameRunning==1){
		mes "เกม [^FF0000"+.GameName$ +"^000000] ได้ถูกสร้างขึ้นแล้ว";
		mes "หากท่านสนใจที่จะเข้าร่วม";
		mes "ให้เข้าห้องสนทนาข้างๆ";
		mes "หรือรอให้จบการแข่งครั้งนี้ก่อน";
		close;
	}
	if($@GameRunning==2){
		mes "สนามแข่งขันยังไม่ว่าง";
		mes "กำลังมีการแข่งเกม [^FF0000"+.GameName$ +"^000000]";
		mes "สนามแข่งขันยังไม่ว่าง";
		close;
	}
	mes "ข้าคือผู้จัดการแข่งขัน";
	mes "Battleground Game";
	mes "ท่านจะเล่นเกมแบบไหน";
	.@slc = select("- แข่งตีหินกิล","- บุกทำหลายหิน","- ช่วยตัวประกัน","- ไม่อยากเล่น");
	next;
	switch(.@slc){
	case 1:
			set $@GamePlay,1;
			.GameName$ = "แข่งตีหินกิล";
			break;
	case 2:
			set $@GamePlay,2;
			.GameName$ = "บุกทำหลายหิน";
			break;
	case 3:
			set $@GamePlay,3;
			.GameName$ = "ช่วยตัวประกัน";
			break;
	case 4:
			mes "^0B7E54[ Battle Organizer ]^000000";
			mes "ข้าจะรออยู่แถวนี้";
			mes "เผื่อท่านเปลี่ยนใจ";
			close;
			break;
	}
	mes "^0B7E54[ Battle Organizer ]^000000";
	mes "ท่านมีเวลา "+$@WaitingTime+" นาที ";
	mes "ในการหาเพื่อนร่วมแข่งขัน";
	mes "เกมจะถูกยกเลิกอัตโนมัติ";
	next;
	mes "^0B7E54[ Battle Organizer ]^000000";
	mes "ท่านต้องการสร้างเกม ^FF0000"+.GameName$+"^000000";
	mes "ค่าบริการ ^FF00001M^000000 zeny";
	mes "ตกลงมั๊ย?";
	if(select("เอาสิ","ไม่ตกลง")==2){
			next;
			mes "^0B7E54[ Battle Organizer ]^000000";
			mes "ข้าจะรออยู่แถวนี้";
			mes "เผื่อท่านเปลี่ยนใจ";
			close;
	}
	next;
	if(Zeny < 1000000){
		mes "^0B7E54[ Battle Organizer ]^000000";
		mes "ท่านมี zeny ไม่เพียงพอ";
		close;
	}
	set Zeny,Zeny-1000000;
	set $@GameRunning,1;
	enablenpc .rednpcname$;
	enablenpc .bluenpcname$;
	delwaitingroom .rednpcname$;
	delwaitingroom .bluenpcname$;
	donpcevent .rednpcname$ +"::OnStart";
	donpcevent .bluenpcname$ +"::OnStart";
	donpcevent strnpcinfo(3)+"::OnWaiting";
	announce strcharinfo(0) +" ได้ทำการสร้างเกม Battleground แบบ ["+.GameName$+"] ขอเซิญผู้ที่สนใจร่วมแข่งขันได้ที่ Prontera",8;
	close;
	end;
OnWaiting:

	sleep ($@WaitingTime*60000);
	if ($@GamePlay!=0&&$@GameRunning !=2){
		announce "เกมกำลังจะถูกยกเลิกใน",8;
		sleep 1000;
		announce "[5]",8;
		sleep 1000;
		announce "[4]",8;
		sleep 1000;
		announce "[3]",8;
		sleep 1000;
		announce "[2]",8;
		sleep 1000;
		announce "[1]",8;
		if ($@GamePlay!=0&&$@GameRunning !=2) donpcevent strnpcinfo(3)+"::OnInit";
	}
	end;
	
OnInit:
	set $@GamePlay,0;
	set $@GameRunning,0;
	set .rednpcname$, "Red Team Lead#bg_emp";
	set .bluenpcname$, "Blue Team Lead#bg_emp";
	delwaitingroom .rednpcname$;
	delwaitingroom .bluenpcname$;
	disablenpc .rednpcname$;
	disablenpc .bluenpcname$;
	end;
}

bat_a01,162,50,4	script	Red Angel#bat	1766,{
	set .@tt,getcharid(4);
	set .@win1x,168;
	set .@win1y,329;
	.@tt %= 2;
	if(.@tt==0) .@tt =2;
	if(.@tt==1){
		getmapxy(.@map$,.@x1,.@y1,0);
		npcwalkto .@x1,.@y1;
		npctalk $@montext$[rand(0,5)];
		if((.@x1 <.@win1x) && (.@y1 > .@win1y)){	
			npctalk "ถึงบ้านแล้วววว ข้าปลอดภัยแล้ว";
			mapannounce "bat_a01", strcharinfo(0) +" ได้ช่วยตัวประกันจากทีม [Blue] แล้ว", bc_map;
			donpcevent "bg_emp#control::OnBlueDown";
		}
	}
	end;
}
bat_a01,171,346,4	script	Blue Angel#bat	1767,{
	set .@win2x,168;
	set .@win2y,67;
	set .@tt,getcharid(4);
	.@tt %= 2;
	if(.@tt==0) .@tt =2;
	if(.@tt==2){
		getmapxy(.@map$,.@x2,.@y2,0);
		npcwalkto .@x2,.@y2;
		npctalk $@montext$[rand(0,5)];
		if(.@x2 <.@win2x && .@y2 < .@win2y){
			npctalk "ถึงบ้านแล้วววว ข้าปลอดภัยแล้ว";
			mapannounce "bat_a01", strcharinfo(0) +" ได้ช่วยตัวประกันจากทีม [Red] แล้ว", bc_map;
			donpcevent "bg_emp#control::OnRedDown";
		}
	}
	end;
}


bat_a01	mapflag	battleground
bat_a01	mapflag	nosave	SavePoint
bat_a01	mapflag	nowarp
bat_a01	mapflag	nowarpto
bat_a01	mapflag	noteleport
bat_a01	mapflag	nomemo
bat_a01	mapflag	nopenalty
bat_a01	mapflag	nobranch
bat_a01	mapflag	noicewall
bat_a01	mapflag	hidemobhpbar
bat_a01	mapflag	nodrop

//bat_a01,0,0	monster	Banshee Master	1974,22
//bat_a01,0,0	monster	Beholder master	1975,22,5000
//bat_a01,0,0	monster	Aunoe	1796,22,5000
//bat_a01,0,0	monster	Necromancer	1870,18
//bat_a01,0,0	monster	Fanat	1797,5,5000