//===== rAthena Script =======================================
//= Sign Your Items
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.1a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Write you name on your rare equipment or weapon ^_-
//===== Additional Comments: =================================
//= 1.1 Cleaned and edited for general use. [Euphy]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================

prt_in,24,61,7	script	Perchik	47,{
	
	setarray .@Item[0],644,3;	// Item requirements: <ID>,<Count>{,...} (0 to disable)
	setarray .@Cost[0],0,5000;	// Zeny requirements: <base price>,<price per refine>

	disable_items;
	mes "[Perchik]";
	mes "ข้าสามารถ ^0055FFสลักชื่อของท่าน^000000 ลงบนไอเท็มที่คุณถืออยู่";
	next;
	if(select("บอกข้ามากกว่านี้หน่อย...:สลักชื่อบนไอเท็มของข้าเลย!") == 1) {
		mes "[เพอร์ชิค]";;
		mes "ข้าสามารถนำชื่อของท่านไปยังช่องที่ว่างอยู่ในอุปกรณ์";
		emotion e_ic;
		next;
		mes "[เพอร์ชิค]";;
		if (getarraysize(.@Item) || getarraysize(.@Cost)) {
			mes "สำหรับงานที่ข้ารับ:";
			if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
				mes " ~ "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (.@Cost[0]) mes " ~ "+callfunc("F_InsertComma",.@Cost[0])+" เซนี";
			if (.@Cost[1]) mes " ~ "+callfunc("F_InsertComma",.@Cost[1])+" เซนีต่อการตีบวก";
		}
		else mes "ข้าสามารถทำให้ฟรี ๆ ได้นะแต่...";
		next;
		emotion e_cry;
		mes "[Perchik]";
		mes "ข้ามีลูกตั้ง 12 คน และเมียที่แสนใจร้าย";
		next;
		mes "[เพอร์ชิค]";;
		mes "หรือลูกที่แสนโหด 12 คน กับเมียที่หิวโหย...";
		emotion e_hmm;
		close;
	}
	mes "[Perchik]";
	mes "แสดงไอเท็มที่ท่านต้องการมาเลย...";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i]))
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) +" [^0055FF"+getequipname(.@indices[.@i])+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, .@indices[ select(.@menu$) ];
	set .@id, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[Perchik]";
	if (!getequipisequiped(.@part)) {
		mes "ไม่มีอะไรที่สลักได้นะ!";
		emotion e_wah;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "โอว ไอเท็มชิ้นนี้มีชื่อสลักอยู่แล้ว ข้าจะไม่จับต้องงานที่ยอดเยี่ยมนี้";
		emotion e_hmm;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "การ์ดรึ! โอ้ว?!";
		mes "อย่างที่ข้าบอก ข้าไม่สลักชื่อให้กับไอเท็มที่มีการ์ดอยู่แล้ว";
		emotion e_hmm;
		close;
	}
	if (getiteminfo(.@id,10)) {
		mes "ขอโทษนะ ข้าไม่สลักชื่อให้ไอเท็มที่ไม่มีรูเหลือ";
		emotion e_sry;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "ขอโทษนะ ข้าไม่สลักชื่อให้กับไอเท็มเช่า!";
			emotion e_hmm;
			close;
		}
	set .@price, .@Cost[0]+(.@Cost[1]*.@ref);
	if (getarraysize(.@Item) || .@price) {
		mes "ข้าต้องการ:";
		if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2) {
			mes " ~ "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (countitem(.@Item[.@i]) < .@Item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes " ~ "+.@price+" เซนี";
	}
	mes "ให้ข้าสลักชื่อ ^0055FF"+getitemname(.@id)+"^000000?";
	next;
	if(select("โอเคเลย!:ไม่เอาไม่สลัก")==2) {
		mes "[Perchik]";
		mes "เจอกัน...";
		emotion e_yawn;
		close;
	}
	mes "[Perchik]";
	if (Zeny < .@price || .@nr) {
		mes "ข้าไม่ทำงานให้กับคำขอบคุณหรอกนะ";
		emotion e_sry;
		close;
	}
	if (!countitem2(.@id,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "ไหน "+getitemname(@id)+"...?";
		npctalk "เพอร์ชิค : คุณมันไอ้ขี้โกง!";
		logmes "Hack: Tried to sign an item not having it: "+getitemname(@id);
		emotion e_wah;
		close;
	}
	if (.@price) set Zeny, Zeny-.@price;
	if (getarraysize(.@Item))  for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
		delitem .@Item[.@i], .@Item[.@i+1];
	delitem2 .@id,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "เสร็จแล้ว!";
	emotion e_proud;
	getitem2 .@id,1,1,.@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	equip .@id;
	close;
}
